import Mathlib.Analysis.Complex.AbsMax
import Mathlib.Analysis.Complex.TaylorSeries
import Mathlib.Analysis.Complex.MeanValue

open Metric Real Complex Filter Topology

open scoped Nat ComplexConjugate

variable {r R M : ℝ} {c z : ℂ} {f : ℂ → ℂ} {n : ℕ}

private lemma circleAverage_eq_iteratedDeriv_div_factorial (hf : DiffContOnCl ℂ f (ball c R)) (hR : 0 < R) :
    circleAverage (fun z ↦ f z / (z - c) ^ n) c R = iteratedDeriv n f c / n ! := by
  rw [circleAverage_eq_circleIntegral hR.ne.symm, inv_smul_eq_iff₀ (by simp)]
  convert hf.circleIntegral_one_div_sub_center_pow_smul hR n using 1
  · congr
    ext
    simp [pow_succ]
    field
  · simp; field

theorem norm_circleAverage_le_circleAverage_norm {E : Type*} {f : ℂ → E} [NormedAddCommGroup E] [NormedSpace ℝ E] :
    ‖circleAverage f c R‖ ≤ circleAverage (fun z ↦ ‖f z‖) c R := by
  simp only  [circleAverage_def, norm_smul, smul_eq_mul]
  gcongr
  · simp [abs_of_nonneg pi_nonneg]
  exact intervalIntegral.norm_integral_le_integral_norm (by positivity)

lemma circleAverage_fun_conj (hf : CircleIntegrable f c R) :
    circleAverage (fun z ↦ conj (f z)) c R = conj (circleAverage f c R) :=
  conjCLE.toContinuousLinearMap.circleAverage_comp_comm hf

private lemma circleAverage_conj_div_sub_pow_eq_zero (hR : 0 < R)
    (hf : DiffContOnCl ℂ f (ball c R)) (hn : n ≠ 0) :
    circleAverage (fun z ↦ conj (f z) / (z - c) ^ n) c R = 0 := by
  calc
  _ = circleAverage (fun z ↦ (1 / ((R : ℂ) ^ 2) ^ n) • conj (f z * (z - c) ^ n)) c R := by
    refine circleAverage_congr_sphere fun z hz ↦ ?_
    simp only [map_mul, map_pow, smul_eq_mul]
    suffices conj (z - c) = R ^ 2 / (z - c) by
      rw [this, div_pow]
      field [pow_ne_zero _ (ofReal_ne_zero.mpr hR.ne')]
    simp only [abs_of_pos hR, mem_sphere_iff_norm] at hz
    rw [eq_div_iff (fun _ ↦ (by simp_all)), mul_comm, mul_conj, normSq_eq_norm_sq, hz, ofReal_pow]
  _ = (1 / ((R : ℂ) ^ 2) ^ n) * circleAverage (fun z ↦ conj (f z * (z - c) ^ n)) c R := by
    rw [circleAverage_fun_smul, smul_eq_mul]
  _ = (1 / ((R : ℂ) ^ 2) ^ n) * conj (circleAverage (fun z ↦ f z * (z - c) ^ n) c R) := by
    rw [circleAverage_fun_conj]
    refine ContinuousOn.circleIntegrable hR.le ?_
    exact (hf.continuousOn_ball.mono sphere_subset_closedBall).mul (by fun_prop)
  _ = _ := by
    rw [DiffContOnCl.circleAverage, sub_self, zero_pow hn, mul_zero, map_zero, mul_zero]
    rw [abs_of_pos hR]
    exact DiffContOnCl.mk_ball (hf.differentiableOn.mul (by fun_prop)) (hf.continuousOn_ball.mul (by fun_prop))

private lemma circleAverage_const_div_sub_pow_eq_zero (a : ℂ) (hR : 0 < R) (hn : n ≠ 0) :
    circleAverage (fun z ↦ a / (z - c) ^ n) c R = 0 := by
  convert circleAverage_eq_iteratedDeriv_div_factorial diffContOnCl_const hR
  simp [iteratedDeriv_const, hn]

private lemma iteratedDeriv_div_factorial_eq_circleAverage_re (M : ℝ) (hf : DiffContOnCl ℂ f (ball c R)) (hR : 0 < R) (hn : n ≠ 0) :
    (iteratedDeriv n f c) / n ! = circleAverage (fun z ↦ ((2 * ((f z).re - M) : ℝ)) / (z - c) ^ n) c R := by
  simp only [mul_sub, ofReal_sub, ← add_conj, add_div, sub_div]
  have cont : ContinuousOn f (sphere c R) := hf.continuousOn_ball.mono sphere_subset_closedBall
  have ne_zero : ∀ x ∈ sphere c R, (x - c) ^ n ≠ 0 := by
    exact fun x hx ↦ pow_ne_zero _ fun h ↦ (by simp_all)
  rw [circleAverage_fun_sub, circleAverage_fun_add, circleAverage_eq_iteratedDeriv_div_factorial hf hR, circleAverage_conj_div_sub_pow_eq_zero hR hf hn, circleAverage_const_div_sub_pow_eq_zero _ hR hn, sub_zero, add_zero]
  all_goals exact ContinuousOn.circleIntegrable hR.le (by fun_prop)

lemma circleAverage_fun_re (hf : CircleIntegrable f c R) :
    circleAverage (fun z ↦ (f z).re) c R = (circleAverage f c R).re :=
  reCLM.circleAverage_comp_comm hf


theorem norm_iteratedDeriv_le_of_re_le_sphere (hR : 0 < R)
    (hf : DiffContOnCl ℂ f (ball c R)) (hf0 : f c = 0)
    (hM : ∀ z ∈ sphere c R, (f z).re ≤ M) (hn : n ≠ 0) :
    ‖iteratedDeriv n f c / (n !)‖ ≤ 2 * M / R ^ n := by
  grw [iteratedDeriv_div_factorial_eq_circleAverage_re M hf hR hn, norm_circleAverage_le_circleAverage_norm]
  apply le_of_eq
  simp only [norm_div, norm_pow, norm_real, Real.norm_eq_abs]
  have hRabs : |R| = R := abs_of_pos hR
  have hfs : ContinuousOn f (sphere c R) := hf.continuousOn_ball.mono sphere_subset_closedBall
  trans circleAverage (fun z => (2 / R ^ n) • (M - (f z).re)) c R
  · refine circleAverage_congr_sphere fun z hz ↦ ?_
    rw [hRabs] at hz
    rw [abs_of_nonpos (by linarith [hM z hz])]
    simp [mem_sphere_iff_norm.mp hz, smul_eq_mul]
    ring
  · rw [← hRabs] at hf
    rw [circleAverage_fun_smul, smul_eq_mul,
          circleAverage_fun_sub (circleIntegrable_const M c R) (ContinuousOn.circleIntegrable hR.le (by fun_prop)), circleAverage_const,
      circleAverage_fun_re (hfs.circleIntegrable hR.le), 
      hf.circleAverage, hf0, zero_re]
    ring

/-- The bound on the derivatives at the centre, assuming only that `f` is holomorphic on the open
disc and that `Re f ≤ M` there : apply the previous estimate on the discs of radius `R' < R` and
let `R'` tend to `R`. -/
theorem norm_iteratedDeriv_le_of_re_le (hR : 0 < R)
    (hf : DifferentiableOn ℂ f (ball c R)) (hf0 : f c = 0)
    (hM : ∀ z ∈ ball c R, (f z).re ≤ M) :
    ‖iteratedDeriv n f c / (n !)‖ ≤ 2 * M / R ^ n := by
  by_cases! hn : n = 0
  · specialize hM c (mem_ball_self hR)
    simp_all
  refine ge_of_tendsto (f := fun R' : ℝ => 2 * M / R' ^ n) (x := 𝓝[<] R)
    (((continuousAt_const.div (by fun_prop) (by positivity)).tendsto).mono_left
      nhdsWithin_le_nhds) ?_
  filter_upwards [self_mem_nhdsWithin, eventually_nhdsWithin_of_eventually_nhds
    (eventually_gt_nhds hR)] with R' hR'R hR'0
  have hsub : closedBall c R' ⊆ ball c R := closedBall_subset_ball hR'R
  exact norm_iteratedDeriv_le_of_re_le_sphere hR'0 (hf.diffContOnCl_ball hsub) hf0
    (fun z hz => hM z (hsub (sphere_subset_closedBall hz))) hn

lemma hasSum_taylorSeries_on_ball_of_eq_zero (hf : DifferentiableOn ℂ f (ball c R))
    (hf0 : f c = 0) (hz : z ∈ ball c R) :
    HasSum (fun n : ℕ ↦ ((n  + 1) ! : ℂ)⁻¹ • (z - c) ^ (n + 1) • iteratedDeriv (n + 1) f c) (f z) := by
  simpa [hf0] using (hasSum_nat_add_iff' 1).mpr (hasSum_taylorSeries_on_ball
    hf hz)

lemma hasSum_taylorSeries_deriv_on_ball (hf : DifferentiableOn ℂ f (ball c R))
    (hz : z ∈ ball c R) :
    HasSum (fun n : ℕ => ((n ! : ℂ))⁻¹ • (z - c) ^ n • iteratedDeriv (n + 1) f c) (deriv f z) := by
  simpa only [← iteratedDeriv_succ'] using Complex.hasSum_taylorSeries_on_ball (hf.deriv isOpen_ball) hz

theorem norm_le_of_re_le (hR : 0 < R)
    (hf : DifferentiableOn ℂ f (ball c R)) (hf0 : f c = 0)
    (hM : ∀ w ∈ ball c R, (f w).re ≤ M) (hz : z ∈ ball c R) :
    ‖f z‖ ≤ 2 * M * ‖z - c‖ / (R - ‖z - c‖) := by
  have hsum := hasSum_taylorSeries_on_ball_of_eq_zero hf hf0 hz
  simp only [mem_ball, dist_eq_norm_sub] at hz
  convert hsum.norm_le_of_bounded ((hasSum_geometric_of_lt_one (by positivity)
    ((div_lt_one hR).mpr hz)).mul_left (2 * M * (‖z - c‖ / R))) fun n ↦ _
  · field
  · simp only [smul_eq_mul]
    rw [mul_comm, mul_assoc, norm_mul, norm_pow]
    calc
    _ = ‖iteratedDeriv (n + 1) f c / ((n + 1) !)‖ * ‖z - c‖ ^ (n + 1) := by field_simp
    _ ≤ _ := by
      grw [norm_iteratedDeriv_le_of_re_le hR hf hf0 hM]
      apply le_of_eq
      rw [pow_succ, pow_succ, div_pow]
      field

/-- **Borel-Carathéodory theorem for the derivative**: under the hypotheses of `norm_le_of_re_le`,
the derivative of `f` on the disc of radius `r < R` is bounded by `2 * R * M / (R - r) ^ 2`. -/
theorem norm_deriv_le_of_re_le (hR : 0 < R)
    (hf : DifferentiableOn ℂ f (ball c R)) (hf0 : f c = 0)
    (hM : ∀ w ∈ ball c R, (f w).re ≤ M) (hz : z ∈ ball c R) :
    ‖deriv f z‖ ≤ 2 * R * M / (R - ‖z - c‖) ^ 2 := by
  have hgeo : HasSum (fun n : ℕ => 2 * M / R * (((n : ℝ) + 1) * (‖z - c‖ / R) ^ n))
      (2 * R * M / (R - ‖z - c‖) ^ 2) := by
    convert! (hasSum_choose_mul_geometric_of_norm_lt_one 1 (r := (‖z - c‖ / R))
      ?_).mul_left (2 * M / R) using 1
    · simp
    · rw [show (1 : ℝ) - ‖z - c‖ / R = (R - ‖z - c‖) / R by field_simp, div_pow]
      field
    · rw [Real.norm_eq_abs, abs_of_nonneg (by positivity)]
      simp only [mem_ball, dist_eq_norm_sub] at hz
      bound
  grw [(hasSum_taylorSeries_deriv_on_ball hf hz).norm_le_of_bounded hgeo fun n ↦ ?_]
  have hfacsucc : (((n + 1)! : ℝ)) = ((n : ℝ) + 1) * (n ! : ℝ) := by
    rw [Nat.factorial_succ]; push_cast; ring
  rw [norm_smul, norm_smul, norm_inv, Complex.norm_natCast, norm_pow, div_pow]
  calc
  _ = ‖z - c‖ ^ n * (n + 1) * (‖iteratedDeriv (n + 1) f c‖ / (n + 1) !) := by
    rw [hfacsucc]
    field
  _ = ‖z - c‖ ^ n * (n + 1) * (‖iteratedDeriv (n + 1) f c / ((n + 1) !)‖) := by simp
  _ ≤ _ := by
    grw [norm_iteratedDeriv_le_of_re_le hR hf hf0 hM]
    exact le_of_eq (by field)
